10 DIM S$(5),R$(6),P(5,9)
20 REM *********************
30 REM * *
40 REM * INSPECTOR CLEW-SO *
50 REM * BY: RON CARLSON *
60 REM * FEB. 1979 *
70 REM * ADAPTED BY *
75 REM * DONALD BROWN *
80 REM * MAY 1979 *
81 REM * *
82 REM * AS FOUND IN *
83 REM *RECREATIONAL *
84 REM * COMPUTING *
85 REM * MAY/JUNE 1979 *
86 REM * *
87 REM *********************
90 S$(1) = "BILL":S$(2) = "MARY":S$(3) = "PAUL":S$(4) = "SUZY":S$(5) = "JOHN"
100 R$(1) = "LOUNGE":R$(2) = "ATRIUM":R$(3) = "LIVING":R$(4) = "DINING":R$(5) = "TROPHY":R$(6) = "GARAGE"
110 GOTO 160
120 ZA$ = LEFT$(ZA$,4): FOR AA = 1 TO 5: IF ZA$ < >S$(AA) THEN NEXT :PP = 0: RETURN
130 PP = AA: RETURN
140 ZA$ = LEFT$(ZA$,6): FOR AA = 1 TO 6: IF ZA$ < >R$(AA) THEN NEXT :PP = 0: RETURN
150 PP = AA: RETURN
160 HOME : VTAB 2: HTAB 12: INVERSE : PRINT "INSPECTOR CLEW-SO": NORMAL : PRINT : PRINT TAB( 13);"BY RON CARLSON"
170 PRINT TAB( 9);"ADAPTED BY DONALD BROWN": PRINT : PRINT
210 PRINT "BILL, MARY, JOHN, SUZY AND PAUL ARE": PRINT "HOUSE GUESTS. THEIR HOST WAS MURDERED": PRINT "BY ONE OF THEM BETWEEN 1 PM. AND 9 PM."
220 PRINT "YOUR JOB AS INSPECTOR CLEW-SO IS TO FIND";: PRINT "THE KILLER, TIME, AND ROOM. YOU WILL": PRINT "BE GIVEN A HOUSE DIAGRAM AND A SET OF"
230 PRINT "QUESTIONS FOR THE SUSPECTS, BUT THE": PRINT "GUILTY PERSON MAY TRY TO MISLEAD YOU BY": PRINT "LYING SOME OF THE TIME."
240 PRINT "IF ONE OF THE SUSPECTS CLAIM THAT THE": PRINT "HOST WAS ALREADY DEAD OR THAT THE HOST": PRINT "WAS STILL ALIVE, THEN YOU HAVE FOUND THE";: PRINT "ROOM WHERE THE MURDER TOOK PLACE.": PRINT
250 PRINT : PRINT "(HIT ANY KEY TO CONTINUE)";: GET A$: PRINT : HOME
260 PRINT "HOUSE DIAGRAM--","O (WINDOWS)"
310 H = 0:C = 0:C1 = 0: PRINT "!---------------------------!": PRINT "! ! O GARAGE !": PRINT "! O TROPHY! !": PRINT "! DINING !---!- -! -- !"
320 PRINT "! !------!": PRINT "!--O----------! -! !": PRINT "! !--!": PRINT "! !- ------ !"
330 PRINT "! LIVING ---! ! !": PRINT "!-- ! O LOUNGE !": PRINT " ! O ATRIUM ! ----!"
340 PRINT " !--------!---------!-----!"
350 PRINT : POKE 34, PEEK(37)
360 VTAB 1: HTAB 33: PRINT "SUSPECTS": FOR AA = 1 TO 5: HTAB 35: PRINT S$(AA): NEXT
400 HOME
460 FOR K = 1 TO 5:P(K,1) = INT(6 * RND(1)) +1: NEXT K: FOR L = 2 TO 9: FOR K = 1 TO 5
510 A = INT(6 * RND(K)) +1: IF A = P(K,L -1) THEN 510
520 P(K,L) = A: NEXT K,L
560 REM ESTABLISHED SUSPECT'S
565 REM MOVEMENTS
570 REM RANDOM ASSIGNMENT OF
575 REM KILLER,TIME,ROOM
580 M = INT(5 * RND(1)) +1:T = INT(9 * RND(2)) +1:R = P(M,T)
610 PRINT : PRINT "INSPECTOR CLEW-SO, WHO DO YOU WANT TO": INPUT " QUESTION? ";S1$
615 IF S1$ = "CRIME" OR LEFT$(S1$,3) = "SOL" THEN 1200
620 ZA$ = S1$: GOSUB 120:S = PP: IF S = 0 THEN 610
630 HOME
640 PRINT "DO YOU WISH TO QUESTION ";S$(S): PRINT "ABOUT--": PRINT " 1 - WHEREABOUTS AT PARTICULAR TIME": PRINT " 2 - TIME SUSPECT IN CERTAIN ROOM"
680 INPUT " 3 - THE CRIME IS SOLVED -- ";A: IF A <1 OR A >3 OR A < > INT(A) THEN 630
700 C = C +1: ON A GOTO 720,990,1200
720 HOME : PRINT S$(S);", ";
740 INPUT "WHERE WERE YOU AT ";T1: IF T1 <1 OR T1 >9 THEN 740
750 PRINT
760 R1 = P(S,T1): IF S < >M THEN 860
780 REM LIAR SECTION
790 IF RND(2) <.5 THEN 910
800 R1 = INT(6 * RND(3)) +1: IF RND(4) <.5 THEN 840
820 PRINT "THE HOST WAS ALREADY DEAD.": GOTO 910
840 PRINT "OUR HOST WAS STILL ALIVE.": GOTO 910
860 IF R1 < >R THEN 910
880 IF RND(5) <.5 THEN 910
890 IF T1 T THEN PRINT " THE HOST WAS ALREADY DEAD."
910 PRINT "I WAS IN THE ";R$(R1);" ROOM."
920 FOR K = 1 TO 5: IF K = S THEN 960
940 IF P(K,T1) = R1 THEN PRINT "I WAS WITH ";S$(K)
950 IF ABS(R1 -P(K,T1)) = 1 THEN PRINT "I SAW ";S$(K)
960 NEXT K: GOTO 610
980 REM ROOM QUESTIONING
990 HOME : PRINT S$(S);",";
1000 INPUT "WHEN WERE YOU IN THE ";ZA$:ZA$ = LEFT$(ZA$,6)
1010 GOSUB 140:T1 = PP
1020 IF T1 = 0 THEN 1000
1025 PRINT
1030 IF S < >M THEN 1110
1040 IF RND(5) <.5 THEN 1110
1050 T1 = INT( RND(5) *6) +1
1090 PRINT "I WAS NOT IN THAT ROOM.": GOTO 610
1110 K = 0: FOR B = 1 TO 9: IF P(S,B) = T1 THEN PRINT "I WAS IN THAT ROOM AT ";B:K = 1
1120 NEXT : IF K = 0 THEN PRINT "I WAS NOT IN THAT ROOM"
1130 GOTO 610
1190 REM CONFRONTATION SECTION
1200 C1 = C1 +1
1205 HOME
1210 PRINT "INSPECTOR DO YOU THINK YOU KNOW :"
1220 PRINT " 1 KILLER": PRINT " 2 ROOM": PRINT " 3 TIME": INPUT " 4 TOTALLY BAFFLED ---";A
1250 IF A <1 OR A >4 OR A < > INT(A) THEN 1210
1260 ON A GOTO 1270,1420,1350,1550
1270 INPUT " THE KILLER IS ? ";ZA$: GOSUB 120: IF PP = 0 THEN 1210
1280 IF PP < >M THEN 1530
1285 VTAB M +2: HTAB 35: INVERSE : PRINT S$(M): NORMAL : HOME
1290 PRINT " YOU HAVE THE KILLER, INSPECTOR CLEW-SO":H = H +1: IF H/2 = INT(H/2) THEN PRINT "YOU HAD HIM BEFORE, REMEMBER?":H = H -1
1300 IF H = 7 THEN 1500
1340 GOTO 610
1350 INPUT "TIME OF THE MURDER ";T1: IF T1 <1 OR T1 >9 OR T1 < > INT(T1) THEN 1210
1360 IF T1 < >T THEN 1530
1370 VTAB 9: HTAB 34: INVERSE : PRINT "TIME:";T1: NORMAL : HOME
1380 PRINT " YOU HAVE THE RIGHT TIME.":H = H +2: IF INT(H/2)/2 = INT(H/4) THEN PRINT "YOU HAD IT BEFORE, DUMMY!":H = H -2
1390 IF H = 7 THEN 1500
1400 GOTO 610
1420 INPUT "ROOM OF THE MURDER?";ZA$: GOSUB 140: IF PP = 0 THEN 1420
1430 IF PP < >R THEN 1530
1435 VTAB 11: HTAB 34: INVERSE : PRINT R$(R): HTAB 35: PRINT "ROOM": NORMAL : HOME
1440 PRINT " YOU HAVE THE RIGHT ROOM":H = H +4: IF H >7 THEN PRINT "I TOLD IT WAS RIGHT THE LAST TIME.":H = H -4
1450 IF H = 7 THEN 1500
1470 GOTO 610
1500 PRINT : PRINT "YOU ARE BRILLIANT, INSPECTOR CLEW-SO": PRINT "IT TOOK YOU ";C;" QUESTIONS AND ";C1: PRINT " CONFRONTATIONS.": GOTO 1590
1530 PRINT "INSPECTOR CLEW-SO YOU ARE A BUMBLING IDIOT. TRY AGAIN.": PRINT : GOTO 610
1550 PRINT "TOO BAD INSPECTOR CLEW-SO": PRINT "THE FACTS ARE:": PRINT " ";S$(M);" KILLED THE HOST": PRINT " AT ";T;" O'CLOCK IN THE": PRINT " ";R$(R);" ROOM"
1590 PRINT : INPUT "DO YOU WANT A NEW CASE?";A$: TEXT : HOME : IF LEFT$(A$,1) = "Y" THEN 260
1600 VTAB 5: PRINT "I HOPE YOU'VE ENJOYED YOURSELF"
1610 FOR V1 = 1 TO 9000: NEXT : PRINT : PRINT CHR$(4)"-STARTUP" 
