10 DIM S$(5), R$(6), P(5,9)
20 REM *********************
30 REM *                   *
40 REM * INSPECTOR CLEW-SO *
50 REM * BY: RON CARLSON   *
60 REM * FEB. 1979         *
70 REM * ADAPTED BY        *
75 REM * DONALD BROWN      *
80 REM * MAY 1979          *
81 REM *                   *
82 REM * AS FOUND IN       *
83 REM * RECREATIONAL      *
84 REM * COMPUTING         *
85 REM * MAY/JUNE 1979     *
86 REM *                   *
87 REM *********************
90 S$(1) = "BILL"
91 S$(2) = "MARY"
92 S$(3) = "PAUL"
93 S$(4) = "SUZY"
94 S$(5) = "JOHN"
100 R$(1) = "LOUNGE"
101 R$(2) = "ATRIUM"
102 R$(3) = "LIVING"
103 R$(4) = "DINING"
104 R$(5) = "TROPHY"
105 R$(6) = "GARAGE"
110 GOTO 160
120 ZA$ = LEFT$(ZA$,4)
121 FOR AA = 1 TO 5
122 IF ZA$ = S$(AA) THEN 130
123 NEXT
124 PP = 0
125 RETURN
130 PP = AA
131 RETURN
140 ZA$ = LEFT$(ZA$,6)
141 FOR AA = 1 TO 6
142 IF ZA$ = R$(AA) THEN 150
143 NEXT
144 PP = 0
145 RETURN
150 PP = AA
151 RETURN
160 PRINT "BILL, MARY, JOHN, SUZY AND PAUL ARE HOUSE GUESTS. THEIR HOST"
170 PRINT "WAS MURDERED BY ONE OF THEM BETWEEN 1 PM. AND 9 PM."
180 PRINT "YOUR JOB AS INSPECTOR CLEW-SO, IS TO FIND THE KILLER, TIME & ROOM."
190 PRINT "YOU WILL BE GIVEN A HOUSE DIAGRAM AND A SET OF QUESTIONS"
200 PRINT "FOR THE SUSPECTS, BUT THE GUILTY PERSON MAY TRY TO MISLEAD YOU,"
210 PRINT "BY LYING SOME OF THE TIME."
220 PRINT "IF ONE OF THE SUSPECTS CLAIM THAT THE HOST WAS ALREADY DEAD,"
230 PRINT "OR THAT THE HOST WAS STILL ALIVE, THEN YOU HAVE FOUND THE"
240 PRINT "ROOM WHERE THE MURDER TOOK PLACE."
250 PRINT
310 H = 0
311 C = 0
312 C1 = 0
313 PRINT "|-----------------------------|"
314 PRINT "|         |         O GARAGE  |"
315 PRINT "|         O TROPHY  |         |"
316 PRINT "| DINING  |---|--  -|  --     |"
320 PRINT "|                       |-----|   O (WINDOWS)"
321 PRINT "|--O----------|        -|     |"
322 PRINT "|                             |---"
323 PRINT "|             |--  -------       |"
330 PRINT "| LIVING   ---|         |        |"
331 PRINT "|--        |            O LOUNGE |"
332 PRINT "  |        O  ATRIUM    |    ----|"
340 PRINT "  |--------|------------|----|"
460 FOR K = 1 TO 5
461 P(K,1) = INT(6 * RND(1)) +1
462 NEXT K
463 FOR L = 2 TO 9
464 FOR K = 1 TO 5
510 A = INT(6 * RND(K)) +1
511 IF A = P(K,L -1) THEN 510
520 P(K,L) = A
521 NEXT K
522 NEXT L
560 REM ESTABLISHED SUSPECT'S
565 REM MOVEMENTS
570 REM RANDOM ASSIGNMENT OF
575 REM KILLER,TIME,ROOM
580 M = INT(5 * RND(1)) +1
581 T = INT(9 * RND(2)) +1
582 R = P(M,T)
610 PRINT
611 PRINT "INSPECTOR CLEW-SO, WHO DO YOU QUESTION";
612 INPUT " ? ",S1$
615 IF S1$ = "CRIME" OR LEFT$(S1$,3) = "SOL" THEN 1200
620 ZA$ = S1$
621 GOSUB 120
622 S = PP
623 IF S = 0 THEN 610
630 PRINT
631 PRINT "DO YOU WISH TO QUESTION ";S$(S);" ABOUT :"
632 PRINT "   1 - THE SUSPECTS WHEREABOUTS AT A PARTICULAR TIME"
633 PRINT "   2 - WHAT TIME THE SUSPECT WAS IN A CERTAIN ROOM"
680 INPUT "   3 - THE CRIME IS SOLVED --- ",A
681 IF A < 1 OR A > 3 OR A <> INT(A) THEN 630
700 C = C + 1
701 ON A GOTO 720, 990, 1200
720 PRINT S$(S);", ";
740 INPUT "WHERE WERE YOU AT ",T1
741 IF T1 <1 OR T1 >9 THEN 740
750 PRINT
760 R1 = P(S,T1)
761 IF S <> M THEN 860
780 REM LIAR SECTION
790 IF RND(2) <.5 THEN 910
800 R1 = INT(6 * RND(3)) +1
801 IF RND(4) <.5 THEN 840
820 PRINT "THE HOST WAS ALREADY DEAD."
821 GOTO 910
840 PRINT "OUR HOST WAS STILL ALIVE."
841 GOTO 910
860 IF R1 <> R THEN 910
880 IF RND(5) <.5 THEN 910
890 IF T1 T THEN PRINT " THE HOST WAS ALREADY DEAD."
910 PRINT "I WAS IN THE ";R$(R1);" ROOM."
920 FOR K = 1 TO 5
921 IF K = S THEN 960
940 IF P(K,T1) = R1 THEN PRINT "I WAS WITH ";S$(K)
950 IF ABS(R1 -P(K,T1)) = 1 THEN PRINT "I SAW ";S$(K)
960 NEXT K
961 GOTO 610
980 REM ROOM QUESTIONING
990 PRINT S$(S);",";
1000 INPUT "WHEN WERE YOU IN THE ",ZA$
1001 ZA$ = LEFT$(ZA$,6)
1010 GOSUB 140
1011 T1 = PP
1020 IF T1 = 0 THEN 1000
1025 PRINT
1030 IF S <> M THEN 1110
1040 IF RND(5) <.5 THEN 1110
1050 T1 = INT( RND(5) *6) +1
1090 PRINT "I WAS NOT IN THAT ROOM."
1091 GOTO 610
1110 K = 0
1111 FOR B = 1 TO 9
1112 IF P(S,B) = T1 THEN PRINT "I WAS IN THAT ROOM AT ";B
1113 K = 1
1120 NEXT
1121 IF K = 0 THEN PRINT "I WAS NOT IN THAT ROOM"
1130 GOTO 610
1190 REM CONFRONTATION SECTION
1200 C1 = C1 +1
1210 PRINT "INSPECTOR DO YOU THINK YOU KNOW :"
1220 PRINT " 1 KILLER"
1221 PRINT " 2 ROOM"
1222 PRINT " 3 TIME"
1223 INPUT " 4 TOTALLY BAFFLED --- ",A
1250 IF A <1 OR A >4 OR A <> INT(A) THEN 1210
1260 ON A GOTO 1270,1420,1350,1550
1270 INPUT " THE KILLER IS ? ",ZA$
1271 GOSUB 120
1272 IF PP = 0 THEN 1210
1280 IF PP <> M THEN 1530
1288 PRINT TAB(35);S$(M)
1290 PRINT " YOU HAVE THE KILLER, INSPECTOR CLEW-SO"
1291 H = H +1
1292 IF H/2 = INT(H/2) THEN PRINT "YOU HAD HIM BEFORE, REMEMBER?"
1293 H = H -1
1300 IF H = 7 THEN 1500
1340 GOTO 610
1350 INPUT "TIME OF THE MURDER ",T1
1351 IF T1 <1 OR T1 >9 OR T1 <> INT(T1) THEN 1210
1360 IF T1 <> T THEN 1530
1373 PRINT TAB(34);"TIME:";T1
1380 PRINT " YOU HAVE THE RIGHT TIME."
1381 H = H +2
1382 IF INT(H/2)/2 = INT(H/4) THEN PRINT "YOU HAD IT BEFORE, DUMMY!"
1383 H = H -2
1390 IF H = 7 THEN 1500
1400 GOTO 610
1420 INPUT "ROOM OF THE MURDER?",ZA$
1421 GOSUB 140
1422 IF PP = 0 THEN 1420
1430 IF PP <> R THEN 1530
1438 PRINT TAB(34);R$(R)
1439 PRINT TAB(35);"ROOM"
1440 PRINT " YOU HAVE THE RIGHT ROOM"
1441 H = H +4
1442 IF H >7 THEN PRINT "I TOLD IT WAS RIGHT THE LAST TIME."
1443 H = H -4
1450 IF H = 7 THEN 1500
1470 GOTO 610
1500 PRINT
1501 PRINT "YOU ARE BRILLIANT, INSPECTOR CLEW-SO"
1502 PRINT "IT TOOK YOU ";C;" QUESTIONS AND ";C1
1503 PRINT " CONFRONTATIONS."
1504 GOTO 1590
1530 PRINT "INSPECTOR CLEW-SO YOU ARE A BUMBLING IDIOT. TRY AGAIN."
1531 PRINT
1532 GOTO 610
1550 PRINT "TOO BAD INSPECTOR CLEW-SO"
1551 PRINT "THE FACTS ARE:"
1552 PRINT " ";S$(M);" KILLED THE HOST"
1553 PRINT " AT ";T;" O'CLOCK IN THE"
1554 PRINT " ";R$(R);" ROOM"
1590 PRINT
1591 INPUT "DO YOU WANT A NEW CASE?",A$
1594 IF LEFT$(A$,1) = "Y" THEN 260
1601 PRINT "I HOPE YOU'VE ENJOYED YOURSELF"
